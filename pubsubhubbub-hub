#!/usr/bin/perl

use strict;
use lib 'lib';
use HTTP::Daemon;
use HTTP::Status;
use Net::PubSubHubbub::Hub;
use Net::PubSubHubbub::Hub::SubscriberState::InMemory;
use Net::PubSubHubbub::Hub::Subscription;
use Getopt::Long;

my $port = 8080;
GetOptions("port=i" => \$port) or die "Unknown options.";

my $d = HTTP::Daemon->new(
    LocalPort => $port,
    ReuseAddr => 1,
    ) or die;

my $base_url = $d->url;
$base_url .= "/" unless $base_url =~ m!/$!;

my $subscriber_state = Net::PubSubHubbub::Hub::SubscriberState::InMemory->new;
my $hub = Net::PubSubHubbub::Hub->new(
    subscriber_state => $subscriber_state,
    );

$subscriber_state->add_subscription(
    Net::PubSubHubbub::Hub::Subscription->new(
	topic => "${base_url}feed.atom",
	callback => "${base_url}/callback",
	expiration => time() + 86400,
    ));

my $ico_data;
if (open(my $fh, "hub.ico")) {
    $ico_data = do { local $/; <$fh>; };
}

print "Hub running at: $base_url\n";
while (my $c = $d->accept) {
    while (my $r = $c->get_request) {
	my $res = favicon_response($r) || $hub->handle_request($r);
	$c->send_response($res);
    }
    $c->close;
    undef($c);
}

sub favicon_response {
    my $req = shift;
    return undef unless $req->method eq "GET" && $req->uri eq "/favicon.ico";
    if ($ico_data) {
	my $res = HTTP::Response->new(200);
	$res->header("Content-Type" => "image/x-icon");
	$res->content($ico_data);
	return $res;
    } else {
	return HTTP::Response->new(404);
    }
}
